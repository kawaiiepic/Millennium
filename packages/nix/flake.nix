{
  description = "Nix Build for Millennium";

  inputs = {
    nixpkgs.url = "github:nixos/nixpkgs?ref=nixos-unstable";

    millennium-src.url = "github:SteamClientHomebrew/Millennium/4722a8ec5824e0425a3d07f2c21935dbf694813f";
    millennium-src.flake = false;

    zlib-src.url = "github:zlib-ng/zlib-ng/2.2.5";
    luajit-src.url = "github:SteamClientHomebrew/LuaJIT/v2.1";
    luajson-src.url = "github:SteamClientHomebrew/LuaJSON/master";
    minhook-src.url = "github:TsudaKageyu/minhook/v1.3.4";
    mini-src.url = "github:metayeti/mINI/0.9.18";
    websocketpp-src.url = "github:zaphoyd/websocketpp/0.8.2";
    fmt-src.url = "github:fmtlib/fmt/12.0.0";
    json-src.url = "github:nlohmann/json/v3.12.0";
    libgit2-src.url = "github:libgit2/libgit2/v1.9.1";
    minizip-src.url = "github:zlib-ng/minizip-ng/4.0.10";
    curl-src.url = "github:curl/curl/curl-8_13_0";
    incbin-src.url = "github:graphitemaster/incbin/main";
    asio-src.url = "github:chriskohlhoff/asio/asio-1-30-0";

    abseil-src.url = "github:abseil/abseil-cpp/20240722.0";
    re2-src.url = "github:google/re2/2025-11-05";

    zlib-src.flake = false;
    luajit-src.flake = false;
    luajson-src.flake = false;
    minhook-src.flake = false;
    mini-src.flake = false;
    websocketpp-src.flake = false;
    fmt-src.flake = false;
    json-src.flake = false;
    libgit2-src.flake = false;
    minizip-src.flake = false;
    curl-src.flake = false;
    incbin-src.flake = false;
    asio-src.flake = false;

    abseil-src.flake = false;
    re2-src.flake = false;
  };

  outputs =
    {
      self,
      nixpkgs,
      millennium-src,
      ...
    }@inputs:
    {
      packages.x86_64-linux =
        let
          pkgs = import nixpkgs {
            system = "x86_64-linux";
            config.allowUnfree = true;
          };
          packages = {
            default = packages.millennium-steam;
            millennium-python = pkgs.callPackage ./python.nix { };
            millennium-assets = pkgs.callPackage ./assets.nix { inherit millennium-src; };
            millennium-frontend = pkgs.callPackage ./frontend.nix { inherit millennium-src; };
            millennium-shims = pkgs.callPackage ./shims.nix { inherit millennium-src; };
            millennium = pkgs.callPackage ./millennium.nix {
              inherit (packages)
                millennium-python
                millennium-shims
                millennium-assets
                millennium-frontend
                ;
              inherit inputs;
            };
            millennium-steam = pkgs.callPackage ./steam.nix {
              inherit (packages)
                millennium-python
                millennium-shims
                millennium-assets
                millennium
                ;
            };
          };
        in
        packages;

      overlays.default = final: prev: {
        inherit (self.packages.${prev.stdenv.hostPlatform.system}) millennium-steam;
      };
    };
}
